buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id "com.wynprice.cursemaven" version "2.1.1"
}

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    removeIf { it instanceof MavenArtifactRepository && it.url.host == 'files.minecraftforge.net' }
    maven {
        url "https://maven.minecraftforge.net/"
    }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    /*maven {
        url 'http://maven.epoxide.xyz'
    }*/
    maven {
        url 'https://dogforce-games.com/maven'
    }
    maven {
        url 'https://maven.mcmoddev.com'
    }
}

version = "1.9.13"
group= "gigaherz.toolbelt" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ToolBelt-1.12.2"

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"

    replace "@VERSION@", project.version
    replaceIn "ToolBelt.java"

    useDepAts = true
}

configurations {
    shade
}

dependencies {
    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"
    shade "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"

    deobfCompile "mezz.jei:jei_1.12.2:4.15.0.283:api"
    runtime "mezz.jei:jei_1.12.2:4.15.0.283"

    deobfCompile "com.azanor:Baubles:1.12-1.5.2"

    deobfCompile "curse.maven:player-weight-mod:2804969"

    compile fileTree(dir: 'libs', include: '*.jar')
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    configurations = [project.configurations.shade]
    relocate 'gigaherz.common', "${project.group}.shadow.commons"
}

reobf {
    shadowJar { }
}

tasks.build.dependsOn reobfShadowJar

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
