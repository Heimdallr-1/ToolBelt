buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'maven-publish'

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
    maven { // JEI
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://maven.tterrag.com/"
    }
    maven { // CURIOS
        url "https://maven.theillusivec4.top/"
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
    mavenLocal()
}

version = "1.16.0"
group = "dev.gigaherz.toolbelt"
archivesBaseName = "ToolBelt-UNKNOWN"

project.afterEvaluate {
    archivesBaseName = "ToolBelt-${project.MC_VERSION}"
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

minecraft {
    mappings channel: 'official', version: '1.16.5'
    //mappings channel: 'parchment', version: '2022.03.06-1.16.5'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            ideaModule "${project.name}.main"

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            ideaModule "${project.name}.main"

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'toolbelt', '--all', '--output', file('src/main/generated_resources/'), '--existing', file('src/main/resources/')

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.31'

    compileOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.152:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.152")

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.16.5-4.0.8.1")
    compileOnly "top.theillusivec4.curios:curios-forge:1.16.5-4.0.8.1"

    compileOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-1.16.5:1.3.2.2")

    // 1.16.5-v4a
    compileOnly fg.deobf("curse.maven:cosmetic-armor-reworked-237307:3540310:api")
    runtimeOnly fg.deobf("curse.maven:cosmetic-armor-reworked-237307:3540310")

    // 2.2.0
    runtimeOnly fg.deobf("curse.maven:curious-armor-stands-398784:3574644")
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "toolbelt",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
